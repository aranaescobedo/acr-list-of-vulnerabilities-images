SecurityResources
| where type == 'microsoft.security/assessments'
| where properties.displayName contains 'Container registry images should have vulnerability findings resolved'
| summarize by assessmentKey = "41503391-efa5-47ee-9282-4eff6131462c" //the ID of the assessment
| join kind=inner (
	SecurityResources
	| where type == 'microsoft.security/assessments/subassessments'
	| extend assessmentKey = extract('.*assessments/(.+?)/.*',1,  id)
) on assessmentKey
| extend parse_json(properties), 
      repositoryName = split(properties.resourceDetails.id,"@")[0],
      severity = properties.status.severity,
      status = properties.status.code
| where status == "Unhealthy" //and repositoryName contains "<ADD-ACR-NAME>" //POSSIBLE TO FILTER ON SPECIFIC ACR (AZURE CONTAINER REGISTRY)
| extend
      containers = parse_json(tostring(parse_json(properties.additionalData.data.Containers)))[0],
      data = properties.additionalData.data,
      high = iff(severity == "High", 3,0), medium = iff(severity == "Medium", 2, 0), low = iff(severity == "Low", 1 ,0)
| extend all = high + medium + low
| order by all desc
| project Namespace = containers.Pod.Namespace,
      Repository = repositoryName,
      ImageTag = data.Tag,
      ScanTime = todatetime(data.ScanTime),
      PodName = containers.Pod.Name,
      Severity = severity,
      Name = properties.displayName,
      Description = properties.description,
      Impact = properties.impact,
	Remediation = properties.remediation,
      Status = status
| order by ScanTime desc
//| where Namespace == "<ADD-NAMESPACE-NAME>" //POSSIBLE TO FILTER ON SPECIFIC NAMESPACE
